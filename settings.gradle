import org.gradle.internal.os.OperatingSystem

rootProject.name = 'jMe3GL2-examples'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            def lwjglNatives = null;
            switch (OperatingSystem.current()) {
                case OperatingSystem.FREE_BSD:
                    lwjglNatives = "natives-freebsd"
                    break
                case OperatingSystem.LINUX:
                    lwjglNatives = "natives-linux"
                    def osArch = System.getProperty("os.arch")
                    if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
                        lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
                    } else if  (osArch.startsWith("ppc")) {
                        lwjglNatives += "-ppc64le"
                    } else if  (osArch.startsWith("riscv")) {
                        lwjglNatives += "-riscv64"
                    }
                    break
                case OperatingSystem.MAC_OS:
                    lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
                    break
                case OperatingSystem.WINDOWS:
                    def osArch = System.getProperty("os.arch")
                    lwjglNatives = osArch.contains("64")
                            ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                            : "natives-windows-x86"
                    break
            }
            version('lwjglNatives', lwjglNatives)
        }
    }
}

include ('dodge_the_creeps')

