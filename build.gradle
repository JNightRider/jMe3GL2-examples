allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = uri("https://jitpack.io") }
        // to find libraries in the project's "lib" directory
        flatDir {
            dirs "${rootProject.projectDir}/lib"
        }
    }
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'idea'

    java {
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

    application {
        if (System.getProperty("os.name").toLowerCase().contains("mac")) {
            applicationDefaultJvmArgs = ['-XstartOnFirstThread'] // this is when using a Mac as a development machine which requires the AppKit main UI thread to be used for GUI applications
        }
    }

    dependencies {
        // You can read more about how to add dependencies here:
        //   https://docs.gradle.org/current/userguide/dependency_management.html#sec:how_to_declare_your_dependencies
        //===----------------------------------------------------------------===
        //                          LWJGL3
        //===----------------------------------------------------------------===
        implementation platform(libs.lwjgl.bom)
        
        // Nuklear
        implementation (libs.bundles.lwjgl.nuklear)
        implementation (libs.bundles.lwjgl.stb)
        implementation (libs.nuclear)

        //===----------------------------------------------------------------===
        //                          jMonkeyEngine3
        //===----------------------------------------------------------------===
        implementation(libs.jme3.core)
        implementation(libs.jme3.desktop)
        runtimeOnly(libs.jme3.jogg)
        runtimeOnly(libs.jme3.plugins)

        implementation(libs.jme3.effects)
        runtimeOnly(libs.jme3.lwjgl3)

        //===----------------------------------------------------------------===
        //                            Lemur GUI
        //===----------------------------------------------------------------===
        implementation(libs.lemur)
        implementation(libs.lemur.proto)
        runtimeOnly(libs.groovy.jsr223)

        //===----------------------------------------------------------------===
        //                            jMe3GL2
        //===----------------------------------------------------------------===
        implementation(libs.jme3gl2.core)
        implementation(libs.jme3gl2.jawt)
        implementation(libs.jme3gl2.dyn4j)
        implementation(libs.jme3gl2.plugins)
        implementation(libs.dyn4j)

        //===----------------------------------------------------------------===
        //                          Other
        //===----------------------------------------------------------------===
        // plugins
        implementation(libs.bundles.log4j)

        implementation(libs.particlemonkey)

        // local
        implementation name: 'jMe3HUDL-1.0-SNAPSHOT'
    }

    // cleanup tasks
    clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
    tasks.register('cleanDLLs', Delete) {
        delete fileTree(dir: '.', include: '*.dll')
    }
    tasks.register('cleanDyLibs', Delete) {
        delete fileTree(dir: '.', include: '*.dylib')
    }
    tasks.register('cleanLogs', Delete) {
        delete fileTree(dir: '.', include: 'hs_err_pid*.log')
    }
    tasks.register('cleanSOs', Delete) {
        delete fileTree(dir: '.', include: '*.so')
    }
}