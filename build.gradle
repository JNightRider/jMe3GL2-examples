allprojects {
    project.ext {
        // global dependencies
        jmonkeyengineVersion = '3.7.0-stable'
        dyn4jVersion = '5.0.2'
    }

    repositories {
        // to find libraries in the project's "lib" directory
        flatDir {
            dirs "${rootProject.projectDir}/lib"
        }
        maven { url "https://jitpack.io" }
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'idea'

    java {
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

    application {
        if (System.getProperty("os.name").toLowerCase().contains("mac")) {
            applicationDefaultJvmArgs = ['-XstartOnFirstThread'] // this is when using a Mac as a development machine which requires the AppKit main UI thread to be used for GUI applications
        }
    }

    dependencies {
        // You can read more about how to add dependencies here:
        //   https://docs.gradle.org/current/userguide/dependency_management.html#sec:how_to_declare_your_dependencies

        implementation 'org.jmonkeyengine:jme3-core:' + jmonkeyengineVersion
        implementation 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
        runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmonkeyengineVersion
        runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion

        implementation 'org.jmonkeyengine:jme3-effects:' + jmonkeyengineVersion
        runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jmonkeyengineVersion

        implementation 'org.dyn4j:dyn4j:' + dyn4jVersion

        implementation 'com.simsilica:lemur:1.16.0'
        implementation 'com.simsilica:lemur-proto:1.13.0'
        runtimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.10'

        implementation 'org.apache.logging.log4j:log4j-core:2.22.0'
        implementation 'org.apache.logging.log4j:log4j-api:2.22.0'

        implementation 'com.epagagames:particlemonkey:1.1.0'

        // local
        implementation name: 'jMe3GL2-core-3.0.0'
        implementation name: 'jMe3GL2-jawt-3.0.0'
        implementation name: 'jMe3GL2-plugins-3.0.0'
        implementation name: 'jMe3HUDL-1.0-SNAPSHOT'
    }

    // cleanup tasks
    clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
    task cleanDLLs(type: Delete) {
        delete fileTree(dir: '.', include: '*.dll')
    }
    task cleanDyLibs(type: Delete) {
        delete fileTree(dir: '.', include: '*.dylib')
    }
    task cleanLogs(type: Delete) {
        delete fileTree(dir: '.', include: 'hs_err_pid*.log')
    }
    task cleanSOs(type: Delete) {
        delete fileTree(dir: '.', include: '*.so')
    }

    task fund(){
        doLast {
           java.awt.Desktop.desktop.browse "https://start.jmonkeyengine.org/#!funding=JME_DESKTOP,JME_EFFECTS,LEMUR,LOG4J2,8113%2F189b56af-a1be-4036-8ac7-2b62a94935ff".toURI()
        }
    }
}